label S_01_C:
    scene TESTSCENE with Dissolve(2.5)
    # oni awakens, the rest of the girls are nearby
    # it's almost lunch, and oni has "taken a nap" after "falling yesterday"
    # the girls chat about lunch and what foods they like most
    # kura brings up that the weather is a little weird recently. Kokoro mentions that it'll be fine, probably. Utagu is nervous because she doesn't have an umbrella. 
    #
    # fade from black to closeup of Oni's eyes
    kura "Oh! You're awake!"
    utagu "Are you alright? You were out for a while there."
    kokoro "She must have been knocked out from that breakfast you made, Kura."
    # eye slowly open
    # fade from Oni to classroom with Kura, Kokoro and Utagu all present 
    show kokoro worry:
        yanchor 1.0 ypos 1080+300 xalign 0.3
    show kura hmm:
        yanchor 1.0 ypos 1080+300 xalign -0.1
    show utagu worry2:
        yanchor 1.0 ypos 1080+300 xalign 0.7

    kokoro "I WARNED you, Oni is too sensitive to any sort of spice. She couldn't even handle black pepper if she tried to!"
    kura "First off, how dare you. Second, she could barely handle white bread if I made her toast."
    utagu "…I think that's just gluten intolerance."
    "As we chatter along, Oni sits up slowly, idly rubbing the sleep from her eye."
    "She looks at me first, and lets her eyes wander around the classroom."
    utagu worry "How are you feeling?"
    show oni neutral_smile:
        yanchor 1.0 ypos 1080+300 xalign 1.2
    oni "I'm fine, thank you. Are you done talking smack about me?"
    "She slowly rises to her feet with a nearby chair."
    "Kokoro and Utagu go to sit by their desks in the center of the classroom as I help Oni up and grab her chair."
    kura frown "Are you sure you're okay?"
    kokoro grin "She's fine, she only took a well earned nap, right?"
    show utagu smile
    show kokoro smile
    "Utagu and Kokoro chatter as Oni sits down in the chair I brought along. I join in and take a seat as well."
    "Our four desks are pushed together in the center of the barren room."
    show utagu uuh
    "Across from me is Utagu, who nervously flicks her eyes to the clock above the blackboard."
    "Anytime she catches me looking at her I feel like I've caught a very sad, lost puppy. She can be too worried for her own good sometimes, but I know she cares about us."
    "To my right Kokoro lounges, idly fiddling with her hair as she waits for our lunch break to arrive."
    "While she prefers to eat my food rather than make any of her own, she does like to help me out in the kitchen. She just loves to snag a bite or few, I guess."
    "Speaking of food…"
    kura smile "It's almost lunch, Oni. I promise I won't poison you this time."
    oni smile "Good thing I didn't miss this one, then."
    "Oni smiles. Her lack of facial expressions can be a little off putting sometimes, but I know she just has a naturally stoic face."
    "She's very relaxing to be with, so I'm glad she's part of our little group." 
    "We sit and waste some time before I decide it's time to go cook."
    "Utagu and Oni sit and chat while Kokoro and I go to prepare for lunch."

    # Transition
    scene TESTSCENE with pixellate
    show kokoro smile:
        yanchor 1.0 ypos 1080+300 xalign 0.3
    show kura smile:
        yanchor 1.0 ypos 1080+300 xalign -0.1
    show utagu smile:
        yanchor 1.0 ypos 1080+300 xalign 0.7
    show oni neutral:
        yanchor 1.0 ypos 1080+300 xalign 1.2

    # school bell ringing
    "The school bell rings out to let us know that our lesson is over."
    "Utagu breathes a sigh of relief."

    kura hmm "What's up, Utagu?"
    utagu worry "Oh, nothing. The homework we got is a bit much."
    kokoro "This assignment is a pain in the butt."
    "…I'm not sure it's the assignment that's making her this gloomy."
    utagu "Just feeling weird today, I guess."
    kura frown "Maybe it's the weather? It looked like it was going to rain this morning."
    kokoro grin "Sorry girls, I checked the forecast and they said it'd be sunny all day. We should be fine, look!"
    "Kokoro smiles widely as she points to the window."
    utagu smile "I hope so. I didn't bring an umbrella today."
    "Utagu sighs again."
    "She opens her eyes a bit more when she spots the bento box I just placed in front of her."
    kokoro "We made your favorite today!"
    kura smile "I made. You tried to snag most of it while I was cooking."
    utagu huh "Oh, did you make karaage?"
    kura smile "Uh huh, here you go!"
    "I hand the box over to Utagu, who cradles it in her arms like it's going to break."
    kokoro smile "Oh, what did I get?"
    kura "And YOU, Kokoro, get this one."
    "She gladly opens the box I just handed to her."
    kokoro laugh "Oooh! Rice balls are my favorite!"
    "We all sit together like earlier this morning. I hand over a bento box to Oni."
    utagu smile "How do you even have time to cook lunch in school?"
    oni neutral_smile "You know how it is here, classes are short enough because it's summer. We have so much time to waste here it's almost like we actually ARE on our break."
    "Kokoro leans over her table towards me, her face beaming a smile."
    kura smile "What about you, Oni?"
    oni neutral "What about what?"
    kura cunning "Lunch, dummy! What do you think is in there?"
    "She ponders for a bit."
    oni neutral_smile "Hmm. With the size and weight of this box I'll say…"
    "I stare at her in anticipation as she hovers the box up and down gently with her hands."
    oni smile "Vegetable tempura with rice and salmon?"
    kura cunning2 "Oh, you're good. Or I'm becoming too predictable."
    oni smile "You just know what I love."

    show oni at transform_hide
    hide oni
    show kokoro at transform_hide
    hide kokoro
    show utagu at transform_hide
    hide utagu
    show kura at transform_hide
    hide kura
    # Fadeout to BG as time passes
    "We all dig in and chat about various topics."
    "Time seems to fly by us in seconds, because as soon as we finish our lunch, afternoon class begins."
    # Transition
    scene TESTSCENE with pixellate
    show kokoro smile:
        yanchor 1.0 ypos 1080+300 xalign 0.3
    show kura smile:
        yanchor 1.0 ypos 1080+300 xalign -0.1
    show utagu smile:
        yanchor 1.0 ypos 1080+300 xalign 0.7
    show oni neutral_smile:
        yanchor 1.0 ypos 1080+300 xalign 1.2

    # Show evening classroom BG w/ all girls
    # school bell ringing
    "We all stretch our arms as the school bell rings."
    kokoro smile_open "Haaaaaah, finally the day is over."
    "I watch her jump out of her chair as I put away my books."
    kokoro "Time for the GOOD stuff to begin!"
    oni neutral "What, reading your gossip magazines and talk about cute girls?"
    kokoro smile "No, stupid! That's for tomorrow! Todaaaay I plan on finding what cookbooks they have hidden around in school."
    kura smug "So you can tell me what to cook for you?"
    kokoro "That too, but I actually want to try and find this old recipe my mom used to make for me when I was a kid. I don't remember much of it except that it was very gooey and sweet, almost like mochi."
    kura hmm "What about you two?"
    "Utagu reacts and looks at me. Oni stares out the window, so I focus on Utagu."
    utagu worry "I uh… I was thinking about going to the library. I wanted to read more about some things for the homework we have."
    "Schoolwork or not, I think she just want to spend some time alone with her books."
    "Oni is still looking outside. I'm unsure how to read her expression."
    kura smile "You going home for the day, Oni?"
    "She looks at me."
    oni "I'll go to the principal's office before I head home. You girls have fun today."
    "She stands up abruptly and leaves the classroom."

    oni smile "See you tomorrow."

    show oni at transform_hide
    hide oni
    show kokoro at transform_hide
    hide kokoro
    show utagu at transform_hide
    hide utagu

    "Everyone tells her goodbye and then goes their own way."
    "I'm left standing alone in the classroom. I have absolutely no idea what to do for the rest of the day."
    "I could help Kokoro out, or go to the library and sit and read something with Utagu."
    "Hmmm…"
    
    label menuChoice:

    menu:
     "Who should I spend time with this afternoon?"

     "Try and find Oni.":
         jump oniroute 

     "Follow Kokoro to the kitchen.":
         jump kokororoute

     "Meet Utagu at the library.":
         jump utaguroute

label oniroute:

    if persistent.kokoroClear and persistent.utaguClear:
        #ONIROUTE
        call S_02_O from _call_S_02_O
        call S_03_O from _call_S_03_O
        call S_04_O from _call_S_04_O
        call S_05_O from _call_S_05_O
        call S_06_O from _call_S_06_O
        call S_07_O from _call_S_07_O
    else:
        scene TESTSCENE_ONI with Dissolve(1.5)
        "I can't seem to find Oni anywhere."
        "I guess I'll hang out with someone else, then."
        jump menuChoice

return

label kokororoute:
    #KOKOROROUTE
    call S_02_K from _call_S_02_K
    call S_03_K from _call_S_03_K
    call S_04_K from _call_S_04_K
    call S_05_K from _call_S_05_K
    call S_06_K from _call_S_06_K
    call S_07_K from _call_S_07_K

return

label utaguroute:
    #UTAGUROUTE
    call S_02_U from _call_S_02_U
    call S_03_U from _call_S_03_U
    call S_04_U from _call_S_04_U
    call S_05_U from _call_S_05_U
    call S_06_U from _call_S_06_U
    call S_07_U from _call_S_07_U
                                                                              
return


